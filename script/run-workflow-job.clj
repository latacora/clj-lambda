(ns run-workflow-job
  "Runs steps for given workflow and job. This can be called within a workflow
  file to test a workflow that has been generated by a previous step. This
  doesn't seem possible with reusable workflows"
  (:require [clj-yaml.core :as yaml]
            [babashka.tasks :refer [shell]]))

(defn -main [args]
  (when-not (= 2 (count args))
    (println "Usage: run-workflow-job.clj WORKFLOW-FILE JOB")
    (System/exit 1))
  (let [[file job] args
        job_ (keyword job)
        steps (->> (slurp file)
                   yaml/parse-string
                   :jobs
                   job_
                   :steps
                   ;; Ignore steps without run as we can't support them and assume
                   ;; they've already been done
                   (filter :run))]
    (doseq [{:keys [run name]} steps]
      (println (format "Step '%s': `%s`" name run))
      (shell run))))

(when (= *file* (System/getProperty "babashka.file"))
  (-main *command-line-args*))
